plugins {
    id "eu.xenit.docker" version "5.5.1"
    id "base"
}

ext.aws_otel_version = '2.11.3'

repositories {
    mavenCentral()
}

def myAttribute = Attribute.of("org.gradle.dependency.bundling", String)

configurations {
    awsOtelAgent {
        attributes {
            attribute(myAttribute, "shadowed")
        }
    }
}

dependencies {
    awsOtelAgent("software.amazon.opentelemetry:aws-opentelemetry-agent:$aws_otel_version@jar")
    attributesSchema {
        attribute(myAttribute)
    }
}

dockerBuild {
    repositories = ['xenit/aws-otel-autoinstrumentation-java']
    tags = [project.ext.aws_otel_version, 'latest']
}

createDockerFile {
    from 'busybox'
    smartCopy configurations.awsOtelAgent.singleFile, "javaagent.jar"
    runCommand("chmod -R go+r /javaagent.jar")
}